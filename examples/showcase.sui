title 'Langium SimpleUI Demo'
icon 'https://langium.org/assets/nib.svg'
topbar 'Langium SimpleUI Demo' styles[background-color: '#d34af3', font-size: '17px', text-color: '#f2f2f2']

linebreak

div classes[flex-container, center]  {
    div classes[border, border--hidden] {
        div classes[center] {
            heading level: 1 'Welcome to our Demo Page for' styles[text-color: 'grey']
           
        } 
        div classes[center]{        
             image 'https://user-images.githubusercontent.com/68400102/150516517-7da9423e-7d0e-4605-91c7-d7693ccd3c28.png' styles[width: '20%', height: "20%"] 
        }     
    }
}

linebreak

usecomponent card("Welcome to our example page", 
"      
This example page of SimpleUI (a DSL build with Langium), 
shows what is possible with Langium. 
This DSL combines HTML, CSS and JavaScript all into one simple language.
")

div classes[flex-container, center] {
    div classes[border, border--hidden, shadow] styles[width: '30%'] {

        heading level: 4 "Function Showcase"
        linebreak
        section {
            paragraph "A simple calculator"
            textbox textBoxOne placeholder: "1" 
            paragraph "+"
            textbox textBoxTwo placeholder: "2" 
            linebreak
            button  'Calculate' { onClick: calculate(getTextbox(textBoxOne), getTextbox(textBoxTwo)) } classes[border] 
        }

        linebreak

        section {
            paragraph "I can also add images with alt"
            image "https://langium.org/assets/langium_logo_w_nib.svg" alt: "Langium Logo" styles[width: '40%', height: "40%"] 
        }
        section {
            paragraph "Links are also supported"
            link "https://langium.org/" text: "Langium"
        }
        
    }
}

linebreak

paragraph "Build with ❤️ and" classes[center] styles[text-color: "rgba(38,136,140)", font-size: '24px'] 
div classes[center]{        
    image 'https://langium.org/assets/langium_logo_w_nib.svg' alt: "Langium Logo" styles[width: '10%', height: "10%"] 
}

function buttonClick() {
    popup 'and Popups to view '
    popup 'also more things in one Function!'
}

function calculate(numberOne: number, numberTwo: number){
    popup $[numberOne] + $[numberTwo]
}


component card (header: string, content: string){
    div classes[flex-container, center] {
        div classes[border, border--hidden, shadow] styles[width: '30%'] {
            heading level: 4 $[header]
            paragraph $[content]
        }
    }
}
